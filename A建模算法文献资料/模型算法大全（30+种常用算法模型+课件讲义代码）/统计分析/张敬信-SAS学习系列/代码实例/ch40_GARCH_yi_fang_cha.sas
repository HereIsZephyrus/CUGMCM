data randar;
     e1=0;
     e11=0;
     do t=-10 to 36;
        e=1.3*e1-0.5*e11+2*rannor(12346);
        x=10+0.5*t+e;
        e11=e1;
        e1=e;
        if t>0 then
           output;
        end;
run;
proc print data=randar;
run;
proc autoreg data=randar PLOTS(ONLY) = FITPLOT;
     model x=t;
run;
proc autoreg data=randar;
     model x=t /dw=4 dwprob;
run;
proc autoreg data=randar;
     model x=t /nlag=2 method=ml;
     output out=pout p=xhat pm=trendhat;
run;
proc gplot data=pout;
     plot x*t=1 xhat*t=2 trendhat*t=3 /overlay;
     symbol1 v=star i=none c=red   h=2.5;
     symbol2 v=plus i=join c=blue  h=2.5;
     symbol3 v=none i=join c=green w=2;
     title1 'Auto-Regression';
     title2 'nlag=2 method=ml';
run;
data new;
     x=. ;
     do t=37 to 46;
        output;
     end;
run;
data randar37;
     merge randar new;
     by t;
run;
proc autoreg data=randar37;
     model x=t /nlag=2 method=ml;
     output out=poutp p=xhat pm=trendhat lcl=lcl ucl=ucl;
run;
proc gplot data=poutp;
     plot x*t=1 xhat*t=2 trendhat*t=3 lcl*t=3 ucl*t=3/overlay href=36.5;
     symbol1 v=star i=none c=red   h=2.5;
     symbol2 v=plus i=join c=blue  h=2.5 l=1;
     symbol3 v=none i=join c=green w=2 ;
     title1 'Auto-Regression:AR(2)';
     title2 'predict estimation';
run;
proc autoreg data=randar;
     model x=t /nlag=5 method=ml backstep;
     title 'Auto-Regression:Backstep';
run;
